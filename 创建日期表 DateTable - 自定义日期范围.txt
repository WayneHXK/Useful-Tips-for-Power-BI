DIM DateTable = 
VAR MinDate = MIN(事实表里的日期列)
VAR MaxDate = MAX(事实表里的日期列)
VAR BaseTable = CALENDAR(MinDate, MaxDate)
RETURN
GENERATE(
    BaseTable,
    VAR currentDay = [Date]
    VAR datekey = FORMAT([Date], "YYYYMMDD")
    VAR year = YEAR(currentDay)
    VAR quarter = "Q" & QUARTER(currentDay)        // 比如Q1
    VAR monthnum = MONTH(currentDay)         // 1位月份数字
    VAR monthnum2 = FORMAT(currentDay, "MM") // 2位月份数字
    VAR monthabbr = FORMAT(currentDay, "MMM")       // 3位月份英文缩写
    VAR monthfullname = FORMAT(currentDay, "MMMM")    // 英文月份全称
    VAR day = DAY(currentDay)                     // 数字几号
    VAR weekid = WEEKDAY(currentDay,1)            // 数字周几
    VAR weekdayname = FORMAT(currentDay, "dddd")  // 英文周几全称
    VAR weekdayabbr = FORMAT(currentDay, "ddd")  // 英文周几缩写
    VAR weeknum = WEEKNUM(currentDay, 1)     // 第几周
    VAR fiscalyearnum = IF(monthnum < 10, year, year+1)        //财年数字
    VAR fiscalyear = "FY" & fiscalyearnum    // 财年带FY
    VAR fiscalquarternum = if(QUARTER(currentDay)=4,1,QUARTER(currentDay)+1)
    VAR fiscalquarter = "Q" & fiscalquarternum // 财季
    VAR fiscalmonthnum = IF(Monthnum >= 10, Monthnum - 9, Monthnum + 3)  //财年的第几个月
   
  RETURN ROW(
   "DateKey", datekey,
   "Year", year,
   "Quarter", quarter,
   "MonthNum", monthnum2,     // 2位月份数字
   "MonthAbbr", monthabbr,    // 3位月份英文缩写
   "MonthName", monthfullname,  // 英文月份全称
   "Day", day,
   "WeekID", weekid,             // 数字周几
   "WeekdayName", weekdayName,   // 英文周几全称
   "WeekdayAbbr", weekdayAbbr,   // 英文周几缩写
   "WeekNum", weekNum,           // 第几周
   "YearMonthSort", year & monthnum2,  // 比如202501
   "Year-Month", year & "-" & monthabbr, // 比如2025-Jan
   "Year-Quarter", year &"-" & quarter,    // 比如2025-Q1
   "FiscalYear", fiscalyear,     // FY2025
   "FiscalMonthNum", fiscalmonthnum,  // 比如2025年12月，对应数字3
   "FiscalYearMonthSort", fiscalyearNum & fiscalmonthnum, //比如202504，对应2025年1月份
   "FicalYear-Month", fiscalyear & "-" & monthabbr,  // 比如 FY2025-Feb
   "FiscalQuarter", fiscalquarter,    // 财季 Q1
   "FiscalQuarterSort", fiscalyearNum & fiscalquarternum, // 比如202501
   "Fiscal-Quarter", fiscalyear & "-" & fiscalquarter    // 财年财季  FY2025-Q1
  )
)
